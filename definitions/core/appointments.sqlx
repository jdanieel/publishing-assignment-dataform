config {
  name: "appointments",
  type: "table",
  schema: "core__acuity",
  tags: ["daily"],
  description: `
      Table containing the most recent state of Acuity appointments
  `,
  assertions: {
    uniqueKey: ["appointment_id", "updated_at"],
    nonNull:   ["appointment_id", "updated_at"]
  },
  bigquery: {
    clusterBy: ["is_current_state"]
  }
}

SELECT
  appointment_id,
  ROW_NUMBER() OVER(
    PARTITION BY appointment_id
    ORDER BY
      updated_at DESC
  ) = 1 AS is_current_state,
  appointment_date,
  CASE
    WHEN canceled IS TRUE THEN 'Canceled'
    WHEN appointment_datetime < CURRENT_TIMESTAMP() THEN 'Completed'
    WHEN appointment_datetime >= CURRENT_TIMESTAMP() THEN 'Scheduled'
  END AS appointment_status,
  start_time,
  end_time,
  date_created,
  datetime_created,
  appointment_datetime,
  appointment_type,
  appointment_type_id,
  class_id,
  addon_id_list,
  category,
  duration,
  TO_HEX(SHA256(email)) AS client_id,
  calendar_id,
  certificate,
  confirmation_page,
  confirmation_page_payment_link,
  location,
  notes,
  timezone,
  calendar_timezone,
  labels,
  forms_text,
  updated_at
FROM
  ${
    ref({
      schema: 'stg__acuity',
      name: "timeline__appointments"
    })
  }
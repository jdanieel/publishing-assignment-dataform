config {
  name: "calendars",
  type: "table",
  schema: "core__acuity",
  tags: ["daily"],
  description: `
      Table containing the most recent state of Acuity calendars
  `,
  assertions: {
    uniqueKey: ["calendar_id", "updated_at"],
    nonNull:   ["calendar_id", "updated_at", "calendar_name"]
  },
  bigquery: {
    clusterBy: ["is_current_state"]
  }
}

SELECT
  calendar_id,
  ROW_NUMBER() OVER(
    PARTITION BY calendar_id
    ORDER BY
      updated_at DESC
  ) = 1 AS is_current_state,
  calendar_name,
  updated_at
FROM
  ${
    ref({
      schema: 'stg__acuity',
      name: "timeline__appointments"
    })
  }

QUALIFY ROW_NUMBER() OVER(
    PARTITION BY calendar_id, updated_at
    ORDER BY
      appointment_id DESC
  ) = 1